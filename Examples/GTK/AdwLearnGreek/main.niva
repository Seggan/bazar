Project use: "org.gnome.adw"


app = Application name: "my.app"

app onActivate: [ 
  args isEmpty => Error throwWithMessage: "Pass path to file with questions"
  path = args at: 0

  window = (org.gnome.adw.ApplicationWindow app: app);
    title: "GTK from Niva";
    x: 250 y: 300
 
  reader = Reader new 


  // READER
  gameModel = reader 
    readFromFile: path
    wordsPerTest: 4 
    invertArgs: false

  pages = gameModel pages

  mut score = 0

  ui = [
    header = org.gnome.adw.HeaderBar new
    allToggles::MutableList::ToggleButton = {}

    resetButton = Button fromIconName: "view-refresh"
    //shuffleButton = Button fromIconName: "media-playlist-shuffle"
    header packStart: resetButton
    //; packStart: shuffleButton


    createPage = [ pageModel::PageModel ->
      hbox = GtkFactory hBoxWithSpacing: 5
      margin = 10
      pairModels = pageModel pairs
            
      
      hbox marginTop: margin;  
        marginBottom: margin;
        marginStart: margin;
        marginEnd: margin
      
      vboxQuestions = GtkFactory vBoxWithSpacing: 5
      vboxAnswers = GtkFactory vBoxWithSpacing: 5

      questionWidgetsList::MutableList::ToggleButton = {}
      answerWidgetsList::MutableList::ToggleButton = {}

      hbox 
        append: vboxQuestions; 
        append: vboxAnswers
      

      // create view models
      pageViewModel = PageViewModel new

      pairModels forEach: [
        answerToggle = ToggleButton withLabel: it answer
        questionToggle = ToggleButton withLabel: it question
        allToggles add: answerToggle; add: questionToggle
        
        x = PairViewModel pairModel: it qWidget: answerToggle aWidget: questionToggle

        // pageViewModel 

        questionWidgetsList add: questionToggle
        answerWidgetsList add: answerToggle

        answerToggle onToggled: [
          questionToggle active => [
            score <- score inc
            "all right!" + score toString |> echo
            questionToggle sensitive: false; addCssClass: "suggested-action"
            answerToggle sensitive: false; addCssClass: "suggested-action"

          ]
        ]

        questionToggle onToggled: [
          answerToggle active => [
            score <- score inc
            "all right!" + score toString |> echo
            questionToggle sensitive: false; addCssClass: "suggested-action"
            answerToggle sensitive: false; addCssClass: "suggested-action"
          ]
        ]
      ]

      questionWidgetsList shuffled forEach: [
        vboxQuestions append: it
      ]
      answerWidgetsList shuffled forEach: [
        vboxAnswers append: it
      ]

      hbox
    ]

    carousel = Carousel new
// add pages
    pages forEach: [
      carousel append: (createPage pageModel: it)
    ]

// reset
    resetButton onClicked: [
      allToggles forEach: [
        it active: false; sensitive: true; removeCssClass: "suggested-action"
      ]

      // right now its not recreating
      // allToggles clear
    ]

    dots = (CarouselIndicatorDots new); carousel: carousel
    

    boxWithCarouselDots = (GtkFactory vBoxExpanded); 
      append: carousel; 
      append: dots


    toolbar = (ToolbarView new); 
      addTopBar: header;
      content: boxWithCarouselDots

    toolbar
  ] do

  window content: ui;
    present
]


app run: args
