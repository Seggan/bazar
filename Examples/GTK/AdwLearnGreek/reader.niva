type Reader 




Reader readFromFile: file::String wordsPerTest::Int invertArgs::Boolean -> GameModel = [

  readCardToLines = [
    text = FileSystem read: file toPath 
    text split: "\n" |> filter: [it trim != ""] |> shuffled
  ]
  
  lines = readCardToLines do
  linesChunked = lines chunked: wordsPerTest

  fillGroups = [ chunk::List::String ->
    pageModel = PageModel new
    
    chunk forEachIndexed: [ i, line ->
      line trim != "" => [
        num = i + 1

        r = line split: ":"
        first = r at: 0 |> trim
        second = r at: 1 |> trim

        "$first $second" echo

        result = invertArgs => 
          PairModel
            question: second
            answer:   first |=> 
          PairModel 
            question: first
            answer:   second 
        

        pageModel add: result
      ]
    ]

    pageModel
  ] 

  gameModel = GameModel new

  linesChunked forEach: [ 
    pageModel = fillGroups chunk: it
    gameModel add: pageModel
    "---" echo
  ]
  
  ^ gameModel
] 



type FileFromDialog

/// open file choose dialog, read file, run lambda on its content
constructor FileFromDialog fromWindow: window::org.gnome.adw.ApplicationWindow doWithContent::[String -> ] -> Unit = [
  fileDialog = FileDialog new
  fileDialog 
    openInside: window 
    ifCancelled: null 
    whenReadyDo: [ x, asyncResult, z -> 
      filePath = fileDialog openFinish: asyncResult |> getPath
      x = FileSystem read: filePath toPath
      x echo
      doWithContent String: x
    ]
]